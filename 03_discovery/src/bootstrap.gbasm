IF !DEF(BOOTSTRAP_GBASM)
DEF BOOTSTRAP_GBASM EQU 0

SECTION "RESERVED", ROM0[$0]

ds $40 - @, 0

SECTION "HEADER", ROM0[$0100]
; that's where the actual ROM header starts, we just reserve until 0x150 for it
; of course we could do more, see the Pan doc for it
; https://gbdev.io/pandocs/The_Cartridge_Header

  jp EntryPoint
  ds $150 - @, $00 ; room for the nintendo logo

SECTION FRAGMENT "PROGRAM", ROM0
Libraries:
INCLUDE "libs/functions.gbasm"
EndLibraries:

SECTION "STARTUPCODE", ROM0[$150]

EntryPoint:
; Shut down audio circuitry
ld a, AUDENA_ON
ld [rAUDENA], a


;; reset RAM to 0
 ld hl, _RAM
 ld bc, GameStateEnd - GlobalVariables
 ld d, 0
 call InitMemChunk
;
 ld hl, _HRAM
 ld bc, lastEntry - FirstHRAMEntry
 ld d, 0
 call InitMemChunk

; enable interrupts
IF (DEF(ENABLE_VBLANKINT) && ENABLE_VBLANKINT == 1)
; setup the timer
ld a, IEF_VBLANK
ldh [rIE], a
xor a
ldh [rIF], a
ei ;; enable the interrupts, because we have defined a non-nop vector there
nop ;; in case
ELSE

ENDC


; can't touch VRAM anything until we're in VBlank, so...
PrepareForVRAMWrite

;.vbl: ld a, [rLY]
;cp SCRN_Y - 10
;jr c, .vbl
; load a palette
ld a, DEFAULT_PALETTE
ld [rBGP], a


; copy tiles
; first, fonts as we're writing text a lot
; then, the rest of the tiles

ld hl, TILES_LOCATION
ld de, FontData
ld bc, FontDataEnd - FontData
call Memcopy

ld hl, GFX_TILES_LOCATION
ld bc, TileDataEnd - TileData
ld de, TileData
call Memcopy


; remember: two adjacent tilemaps exist
; https://gbdev.io/pandocs/Tile_Maps.html#vram-tile-maps
; $9800 -> $9BFF
; $9C00 -> $9FFF
;; this draws the tilemap to VRAM
; copy the base screen
ld hl, $9800
ld de, TitleScreenTM
ld c, (EndTitleScreenTM - TitleScreenTM) / 4
ld b, GFX_TILES_OFFSET ; some tilemaps are built with an offset
call LoadTilemapWithPositiveOffset


; turn on the LCD
ld a, LCDCF_ON | LCDCF_BGON
ld [rLCDC], a

; the gameloop will takeover from there

jp StartGame

ENDC